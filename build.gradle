plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'com.kazurayam'
version '0.2.0'

ext.isReleaseVersion = ! version.endsWith("SNAPSHOT")

repositories {
    mavenCentral()
}


sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }
tasks.withType(GroovyCompile).each {it.groovyOptions.encoding = defaultEncoding }

ext {
    seleniumjavaVersion = '3.141.59'
}

dependencies {

    // https://mvnrepository.com/artifact/ru.yandex.qatools.ashot/ashot
    implementation libs.ashot

    implementation libs.seleniumjava

    // https://mvnrepository.com/artifact/io.github.bonigarcia/webdrivermanager
    testImplementation libs.webdrivermanager

    // https://mvnrepository.com/artifact/org.json/json
    testImplementation libs.gson

    testImplementation libs.junit.jupiter.api
    testRuntimeOnly libs.junit.jupiter.engine

    implementation libs.slf4j.api
    testImplementation libs.slf4j.simple
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                groupId = project.group
                name = project.rootProject.name
                description = 'A wrapper for AShot --- a screenshot utility for WebDriver'
                url = 'https://kazurayam.github.io/ashotwrapper/'
                from components.java
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org.licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kazurayam'
                        name = 'URAYAMA,kazuaki'
                        email = 'kazuaki.urayama@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/kazurayam/${project.rootProject.name}.git"
                    developerConnection = "scm:git:git@github.com:kazurayam/${project.rootProject.name}.git"
                    url = "https://github.com/kazurayam/${project.rootProject.name}"
                }
            }
        }
    }
    repositories {
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo: snapshotRepo
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }
}


signing {
    sign publishing.publications.mavenJava
}
// I do not want to sign the SNAPSHOT
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

task copyJavadocToDocsDir(type: Copy) {
    from 'build/docs/javadoc'
    into 'docs/api'
}
javadoc.finalizedBy copyJavadocToDocsDir